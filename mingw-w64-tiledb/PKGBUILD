# Maintainer: Isaiah Norton <isaiah@tiledb.com>

#set -x

_realname=tiledb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
# update both pkgver and srcver
# - pkgver should be final canonical version
# - for RC testing, use e.g "srcver=2.1.0-rc1" and "pkgver=2.1.0" to
#   avoid name verification errors due to the dash.
pkgver=2.1.0
srcver=2.1.0
pkgrel=2
pkgdesc="Storage management library for sparse and dense array data (mingw-w64)"
arch=("any")
url="https://tiledb.com/"
license=("MIT")
depends=("${MINGW_PACKAGE_PREFIX}-aws-sdk-cpp"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-curl")
options=("staticlibs")

#source=(${_realname}-${srcver}.tar.gz::"https://github.com/TileDB-Inc/TileDB/archive/${srcver}.tar.gz")
#sha256sums=("SKIP")
noextract=(${_realname}-${srcver}.tar.gz)

# switch to git due to bug (?) in build system default tar: fails to un-tar the archive
source=(${_realname}-${pkgver}::"git+https://github.com/TileDB-Inc/TileDB#tag=${srcver}")
sha256sums=("SKIP")


prepare() {
  echo "SOURCE DIR is: ${srcdir}"
  cd "${srcdir}/${_realname}-${srcver}"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  if [ "$CARCH" == "i686" ]; then
  export CFLAGS="-mfpmath=sse -msse2"
  export CXXFLAGS="-mfpmath=sse -msse2"
  fi

  export MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX="
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DTILEDB_STATIC=ON \
    -DTILEDB_TBB=OFF \
    -DTILEDB_S3=ON \
    ../${_realname}-${srcver}
  make
  make -C tiledb
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make  DESTDIR="${pkgdir}" -C tiledb install
  rm -Rf "${pkgdir}${MINGW_PREFIX}/bin"
  rm  "${pkgdir}${MINGW_PREFIX}/lib/libtiledb.dll.a"
}

check (){
  cd ${srcdir}/build-${MINGW_CHOST}
  #make check
}
